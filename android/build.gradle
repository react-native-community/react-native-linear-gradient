import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

def _ext = rootProject.ext;

def _reactNativeVersion = _ext.has('reactNative') ? _ext.reactNative : '+';
def _compileSdkVersion = _ext.has('compileSdkVersion') ? _ext.compileSdkVersion : 26;
def _buildToolsVersion = _ext.has('buildToolsVersion') ? _ext.buildToolsVersion : '26.0.1';
def _minSdkVersion = _ext.has('minSdkVersion') ? _ext.minSdkVersion : 16;
def _targetSdkVersion = _ext.has('targetSdkVersion') ? _ext.targetSdkVersion : 26;

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion

    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    jcenter()
    maven {
        url "$rootDir/../node_modules/react-native/android"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    provided "com.facebook.react:react-native:${_reactNativeVersion}"
}

task generateReactArchive(type: Upload) {
    configuration = configurations.archives
    def jsonFile = file("$rootDir/../package.json")
    def packageJson = new JsonSlurper().parseText(jsonFile.text)
    repositories.mavenDeployer {
        repository url: "file://${rootDir}/repository"
        pom.project {
            groupId 'react-native-community'
            artifactId 'react-native-linear-gradient'
            version packageJson.version
        }
    }
}
